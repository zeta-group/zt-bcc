strict namespace bcs {

#if 1
#ifndef HEAP_SIZE
#define HEAP_SIZE 16384
#endif
#endif

private enum
{
    HEAP_START = HEAP_SIZE - 1 // Don't touch this.
};

raw Heap[HEAP_SIZE];

private struct AllocBlock
{
    struct AllocBlock* next;
    int size;
    bool used;
};

private struct HeapHead
{
    bool init;
    int allocTime;
    struct AllocBlock* tail, lastFreed;
};

// AllocBlock struct
private enum
{
    AB_NEXT,
    AB_SIZE,
    AB_USED,
    AB_SIZEOF // no autBlock flag, not worth cramming in
};

// HeapHead struct
private enum
{
    HH_INIT,
    HH_ALLOCTIME, // used by GDCC version
    HH_TAIL,
    HH_LASTFREED,
    HH_SIZEOF
};

// Main memory allocation function. Returns a pointer to a buffer of the requested size.
private int malloc (int size)
{
    struct HeapHead* head = (int)Heap + (HEAP_START - HH_SIZEOF);
    struct AllocBlock* block;

    if(!head.init)
    {
        head.init = true;

        block = (int)Heap + ((int)head - ((size + AB_SIZEOF) - 1));

        block.used = true;
        block.size = size;
        block.next = 0;

        head.tail = (int)block;

        return (int)block + AB_SIZEOF;
    }

    block = head.lastFreed;

    if( block && (block.size >= size) )
    {
        head.lastFreed = 0;
        block.used = true;
        return (int)block + AB_SIZEOF;
    }

    block = head.tail;

    do
    {
        if(block.used || (size > block.size))
        {
            block = block.next;
            continue;
        }

        if((block.size - size) >= (AB_SIZEOF + 4))
        {
            struct AllocBlock* splitBlock = (int)block + size + AB_SIZEOF;

            splitBlock.used = false;
            splitBlock.size = block.size - AB_SIZEOF - size;
            splitBlock.next = block.next;

            block.next = splitBlock;
            block.size = size;
        }

        block.used = true;

        return (int)block + AB_SIZEOF;

    } while(block);

    // No acceptable blocks found, so let's "grow" the heap.
    block = (int)Heap + ((int)head.tail - (AB_SIZEOF + size));

    block.used = true;
    block.size = size;
    block.next = head.tail;

    head.tail = block;

    return (int)block + AB_SIZEOF;
}

// Frees the memory so it is usable by future calls to smMalloc.
private void free (int ptr)
{
    if(!ptr)
        return;

    struct HeapHead* head;
    struct AllocBlock* block = ptr - AB_SIZEOF;
    struct AllocBlock* nextBlock = block.next;

    // Should merge blocks here.
    if( (nextBlock) && (!nextBlock.used) )
    {
        block.size += nextBlock.size + AB_SIZEOF;
        block.next = nextBlock.next;
    }

    block.used = false;

    head = (int)Heap + (HEAP_START - HH_SIZEOF);
    head.lastFreed = block;
}

// Creates a new bigger buffer if needed, copying the contents of the original passed in.
private int realloc (int oldPtr, int size)
{
    if(!oldPtr)
    {
        if(!size)
            return 0;
        return malloc(size);
    }

    if(!size)
    {
        free(oldPtr);
        return 0;
    }

    struct AllocBlock* oldBlock = oldPtr - AB_SIZEOF;
    if(oldBlock.size >= size)
        return oldPtr;

    int ptr = malloc(size);

    ptr -= (int)Heap;
    oldPtr -= (int)Heap;

    for(int i = oldBlock.size; i > 0; i--)
        Heap[ptr+i] = Heap[oldPtr+i];

    ptr += (int)Heap;
    oldPtr += (int)Heap;

    free(oldPtr);
    return ptr;
}

// Debug print function.
void allocDump ()
{
    struct HeapHead* head = (int)Heap + (HEAP_START - HH_SIZEOF);
    struct AllocBlock* block = head.tail;

    Print(s:"=smAllocDump=");
	while(block)
	{
        Print(i:(int)block - (int)Heap, s:" - next: ", i:(int)block.next - (int)Heap, s:", size: ", x:block.size, s:", used: ", i:(int)block.used & (1<<0), s:", autBlock: ", i:(int)block.used & (1<<2));

        block = block.next;
	}
}

// BCC array references are fat pointers:
// first word is an offset pointing to the first array element
// second word is an offset pointing to the length of the array
// inline ASM is of course required to change the length offset
private void[]* makeArrPtr (int ptr, int len)
{
    void[]* arr;

    >pushscriptvar 0
    >assignscriptvar 2 // Write pointer to array.
    >pushscriptvar 2
    >pushscriptvar 1
    >assignmaparray 0 // Write length to first word of allocated space.
    >pushscriptvar 2
    >assignscriptvar 3 // Set pointer to length.
    >incscriptvar 2 // Finally, move the pointer to array start to actually point to the first element.

    return arr;
}

private void[]* arrNew (int len)
{
    int ptr = malloc(len + 1);

    return makeArrPtr(ptr, len);
}

private void arrDelete (void[]* arr)
{
    free((int)arr - 1);
}

private void[]* arrResize (void[]* arr, int newSize)
{
    int newPtr = realloc((int)arr - 1, newSize + 1);

    if(!newPtr)
    {
        Log(s:"Dynarray WARNING: resize to ", i:newSize, s:" FAILED!");
        return arr;
    }

    return makeArrPtr(newPtr, newSize);
}

#if 1
	#define __printf_parms str string, raw i1 = 0, raw i2 = 0, raw i3 = 0, raw i4 = 0, raw i5 = 0, \
							raw i6 = 0, raw i7 = 0, raw i8 = 0, raw i9 = 0, raw i10 = 0, \
							raw i11 = 0, raw i12 = 0, raw i13 = 0, raw i14 = 0, \
							raw i15 = 0, raw i16 = 0

	#define __printf_args string, i1, i2, i3, i4, i5, i6, i7, i8, i9, i10, i11, i12, i13, i14, i15, i16

#define BCSFMT_ACCURATE_FIXED

#ifdef BCSFMT_ACCURATE_FIXED

private void PrintFixedPrec (fixed n)
{
	str string;
	int integerPart = int(n);
	int fractPart = ((((int)n) & 65535) * 10000) / 65536;

	buildmsg(string = StrParam())
	{
		Append(i:integerPart, c:'.');

		if(fractPart < 1)
		{
			Append(s:"0000");
			goto done;
		}
		else if(fractPart < 10)
			Append(s:"000");
		else if(fractPart < 100)
			Append(s:"00");
		else if(fractPart < 1000)
			Append(s:"0");

		Append(i:fractPart);

		done:
	}

	>pushscriptvar 1
	>printstring
}

#endif
#endif

private void __putc (int c)
{
	>pushscriptvar 0
	>printcharacter
}

private void PrintInt (raw i)
{
	>pushscriptvar 0
	>printnumber
}

private void PrintFixed (raw i)
{
	>pushscriptvar 0
	>PrintFixed
}

#if 1
	#define PUSH_PARAMS_ARRAY >pushscriptarray 0
	#define PUSH_PARAMNUM >pushscriptvar 17
#endif

private void __printf (__printf_parms)
{
	raw params[] = {i1, i2, i3, i4, i5, i6, i7, i8, i9, i10, i11, i12, i13, i14, i15, i16};

	int paramNum;

	int len = strlen(string);

	int ch;

	for(int i = 0; i < len; i++)
	{
		ch = string[i];

		if(ch != '%')
		{
			__putc(ch);
		}
		else
		{
			switch(string[++i])
			{
			//The actual specifiers themselves.
			//(assuming they're checked in this order, I'm putting them
			//first since they alone are more common)
			case 'i':
			case 'd':
				PUSH_PARAMNUM
				PUSH_PARAMS_ARRAY
				>printnumber
				paramNum++;
			break;

			case 'c':
				PUSH_PARAMNUM
				PUSH_PARAMS_ARRAY
				>printcharacter
				paramNum++;
			break;

			case 'f':
			#ifdef BCSFMT_ACCURATE_FIXED
				PrintFixedPrec(params[paramNum++]);
			#else
				PUSH_PARAMNUM
				PUSH_PARAMS_ARRAY
				>printfixed
				paramNum++;
			#endif
			break;

			case 's':
				PUSH_PARAMNUM
				PUSH_PARAMS_ARRAY
				>printstring
				paramNum++;
			break;

			case 'n':
				PUSH_PARAMNUM
				PUSH_PARAMS_ARRAY
				>printname
				paramNum++;
			break;

			case 'x':
				PUSH_PARAMNUM
				PUSH_PARAMS_ARRAY
				>printhex
				paramNum++;
			break;

			case 'b':
				PUSH_PARAMNUM
				PUSH_PARAMS_ARRAY
				>printbinary
				paramNum++;
			break;

			case 'k':
				PUSH_PARAMNUM
				PUSH_PARAMS_ARRAY
				>printbind
				paramNum++;
			break;

			case 'l':
				PUSH_PARAMNUM
				PUSH_PARAMS_ARRAY
				>printlocalized
				paramNum++;
			break;

			case '%':
				__putc('%');
			break;

			default:
				Log(s:"unexpected flag identifier ", c:string[i]);
			return;
			}
		}
	}
}

void printf (__printf_parms)
{
	buildmsg(Print())
		{__printf(__printf_args);}
}

void printboldf (__printf_parms)
{
	buildmsg(PrintBold())
		{__printf(__printf_args);}
}

void logf (__printf_parms)
{
	buildmsg(Log())
		{__printf(__printf_args);}
}

str strformat (__printf_parms)
{
	str retval;

	buildmsg(retval = StrParam())
		{__printf(__printf_args);}

	return retval;
}

#undef __printf_parms
#undef __printf_args

}
